(* This is a ProVerif description of the Protocol Flow from the Verifiable Consent architecture. *)

(* START keys *)
type SecretKey.
type PublicKey.
type SymmetricKey.
fun pk(SecretKey):PublicKey.
(* END keys *)

(* START group *)
type G.
type exponent.
fun exp(G, exponent): G.
fun g(exponent): G.
equation forall x: exponent, y: exponent; exp(g(x),y) = exp(g(y),x).
fun GtoSymK(G): SymmetricKey.
fun bitG(G):bitstring.
reduc forall elem:G; unbitG(bitG(elem))=elem.
(* END group *)

(* START symmetric encryption *)
fun enc(bitstring(*the message*), SymmetricKey):bitstring (*symmetric encrypt input msg*).
reduc forall msg:bitstring,symk:SymmetricKey; dec(enc(msg,symk),symk)=msg (*symmetric decrypt*).
(* END symmetric encryption *)

(* (* START asymmetric encryption *)
fun aenc(bitstring(*the message*), PublicKey):bitstring (*asymmetric encrypt input msg*).
reduc forall msg:bitstring,sk:SecretKey; adec(aenc(msg,pk(sk)),sk)=msg (*asymmetric decrypt*).
(* END Asymmetric encryption *) *)

(* START digital signature *)
fun sig(bitstring(*the message*), SecretKey):bitstring(*the signature value*).
reduc forall msg:bitstring,sk:SecretKey; check(msg,sig(msg,sk),pk(sk))=true (*check(msg,sigVal,pk)*).
(* END digital signature *)

(*DEFS*)

(* Communication *)
free h:channel.

free Client_Completes:bitstring [private].
free Server_Completes:bitstring [private].

(* Agreement Events *)
event auth_ClientSendsLastMessageToServer(bitstring,bitstring,bitstring,bitstring,bitstring).
event auth_ServerCompletesProtocol(bitstring,bitstring,bitstring,bitstring,bitstring).

event auth_ServerSendsLastMessageToClient(bitstring,bitstring,bitstring,bitstring,bitstring,bitstring).
event auth_ClientCompletesProtocol(bitstring,bitstring,bitstring,bitstring,bitstring,bitstring).

event clientReceivedResource(bitstring,bitstring,bitstring).
event serverHasAgreed(bitstring,bitstring,bitstring).
event clientHasAgreed(bitstring,bitstring,bitstring).
event serverHasOffered(bitstring,bitstring,bitstring).




(* START process macros ~ agent *)

(** START Client **)
let client(sk_c:SecretKey, pk_s:PublicKey, talksOnlyToHonest:bool) (* input ~ what the agent starts with in private*) =

    (**)
    new x:exponent;
    let gx = g(x) in
    let m'_0 = bitG(gx) in
    let m_0 = m'_0 in
    out(h, m_0);
    (* Sent out Handshake-Request *)
    (**)
    (**)
    (* Receive Handshake-Response *)
    in(h,m_1:bitstring);
    let (eGY:bitstring,m:bitstring) = m_1 in
    let gy = unbitG(eGY) in
    let K = GtoSymK(exp(gy,x)) in
    let s_K = dec(m,K) in
    if check((gy,gx), s_K, pk_s) then 
    new m_uri:bitstring;
    new nonce_0:bitstring;
    let m'_2 = (sig((gx,gy), sk_c), nonce_0, m_uri, pk_s, pk(sk_c)) in
    let m_2 = enc(m'_2, K) in
    out(h,m_2);
    (* Sent out data request (enc) *)
    (**)
    (**)
    (* Receive data usage offer (enc) *)
    in(h,m_3:bitstring);
    (* a. decrypt *)
    let m'_3 = dec(m_3,K) in
    (* b. deconstruct *)
    let (
        nonce'_0:bitstring, 
        nonce_1:bitstring, 
        pk'_c:PublicKey, 
        pk'_s:PublicKey,
        m'_uri:bitstring, 
        m_shape:bitstring,
        sig_shape:bitstring, 
        m_Aoffer:bitstring, 
        sig_Aoffer:bitstring,
        m_Brequest:bitstring,
        sig_Brequest:bitstring
        ) = m'_3 in
    (* c. check signatures *)
    if check(m_shape, sig_shape, pk_s) then
    if check(m_Aoffer, sig_Aoffer, pk_s) then
    if check(m_Brequest, sig_Brequest, pk_s) then
    (* d. check content *)
    if m'_uri = m_uri then
    if nonce'_0 = nonce_0 then
    if pk'_c = pk(sk_c) then
    (* e. create new message content *)
    let m_presentation:bitstring = m_shape in
    let m_Aagreement:bitstring = m_Aoffer in
    let m_Brequirement:bitstring = m_Brequest in
    new nonce_2:bitstring;
    (* f. sign files *)
    let sig_presentation:bitstring = sig(m_presentation, sk_c) in
    let sig_Aagreement_client:bitstring = sig(m_Aagreement, sk_c) in
    let sig_Brequirement:bitstring = sig(m_Brequirement, sk_c) in
    (* g. encrypt *)
    let m_4:bitstring = enc((nonce_1, nonce_2, m_uri, m_presentation, sig_presentation, m_Aagreement, sig_Aagreement_client, m_Brequirement, sig_Brequirement), K) in
    (* h. send new message *)
    event clientHasAgreed(m_presentation, m_Aagreement, m_Brequirement);
    event auth_ClientSendsLastMessageToServer(m_0, m_1, m_2, m_3, m_4);
    out(h,m_4);


    in(h,m_5:bitstring);
    (* a. decrypt *)
    let m'_5 = dec(m_5,K) in
    (* b. deconstruct *)
    let (
        nonce'_2:bitstring, 
        m''_uri:bitstring, 
        m_resource:bitstring, 
        m_Bagreement:bitstring,
        sig_Bagreement:bitstring,
        m'_Aagreement:bitstring,
        sig'_Aagreement_client:bitstring,
        sig_Aagreement_server:bitstring
        ) = m'_5 in
    (* c. check signatures *)
    if check(m_Bagreement, sig_Bagreement, pk_s) then
    if check(m'_Aagreement, sig_Aagreement_server, pk_s) then
    (* d. check content *)
    if m''_uri = m_uri then
    if nonce'_2 = nonce_2 then
    if m_Bagreement = m_Brequirement then
    if m'_Aagreement = m_Aagreement then
    if sig'_Aagreement_client = sig_Aagreement_server then

    (* Client about to complete protocol! *)
    out(h, Client_Completes);
    if talksOnlyToHonest then
    (* event_at_the_very_end_of_role_R *)
    event clientReceivedResource(m_resource, m_Bagreement, m'_Aagreement);
    event auth_ClientCompletesProtocol(m_0, m_1, m_2, m_3, m_4, m_5) ;
    0.
(** END Client **)

(** START Server **)
let server(sk_s:SecretKey, pk_c:PublicKey, talksOnlyToHonest:bool) (* input ~ what the agent starts with in private*) =

    (**)
    (* Receive Handshake-Request *)
    in(h, m_0:bitstring);
    let gx= unbitG(m_0) in
    new y:exponent;
    let gy = g(y) in
    let K = GtoSymK(exp(gx,y)) in
    let m'_1 = sig((gy,gx),sk_s) in
    let m_1 = (bitG(gy),enc(m'_1, K)) in
    out(h,m_1); 
    (* Sent out Handshake-Response *)
    (**)
    (**)
    (* Receive data request (enc) *)
    in(h,m_2:bitstring);
    (* ab. decrypt deconstruct *)
    let (
        sig_K:bitstring, 
        nonce_0:bitstring, 
        m_uri:bitstring, 
        pk'_s:PublicKey, 
        pk'_c:PublicKey
        ) = dec(m_2,K) in
    if (pk'_s,pk'_c) = (pk(sk_s),pk_c) then
    (* out-of-band - check if public *)
    (* c. check signatures *)
    if check((gx,gy),sig_K,pk_c) then
    (* d. check content *)
    (* if m'_uri = m_uri then *)
    (* e. create new message content *)
    new m_shape:bitstring;
    new m_Aoffer:bitstring;
    new m_Brequest:bitstring;
    new nonce_1:bitstring;
    (* f. sign message content *)
    let sig_shape:bitstring = sig(m_shape, sk_s) in
    let sig_Aoffer:bitstring = sig(m_Aoffer, sk_s) in
    let sig_Brequest:bitstring = sig(m_Brequest, sk_s) in
    (* g. encrypt *)
    let m_3:bitstring = enc((nonce_0, nonce_1, pk_c, pk(sk_s), m_uri, m_shape, sig_shape, m_Aoffer, sig_Aoffer, m_Brequest, sig_Brequest), K) in
    (* h. send new message *)
    event serverHasOffered(m_shape, m_Aoffer, m_Brequest);
    out(h, m_3);
    (* Sent out data usage offer (enc) *)


    in(h,m_4:bitstring);
    (* a. decyrpt *)
    let m'_4 = dec(m_4, K) in
    (* b. deconstruct *)
    let (
        nonce'_1:bitstring, 
        nonce_2:bitstring, 
        m''_uri:bitstring, 
        m_presentation:bitstring, 
        sig_presentation:bitstring,
        m_Aagreement:bitstring,
        sig_Aagreement_client:bitstring,
        m_Brequirement:bitstring,
        sig_Brequirement:bitstring
        ) = m'_4 in
    (* c. check signatures *)
    if check(m_presentation, sig_presentation, pk_c) then
    if check(m_Aagreement, sig_Aagreement_client, pk_c) then
    if check(m_Brequirement, sig_Brequirement, pk_c) then
    (* d. check content *)
    if m''_uri = m_uri then
    if nonce'_1 = nonce_1 then
    if m_Aagreement = m_Aoffer then
    if m_Brequirement = m_Brequest then
    if m_presentation = m_shape then
    (* e. create new message content *)
    new m_resource:bitstring;
    let m_Bagreement:bitstring = m_Brequirement in
    (* f. sign message content *)
    let sig_Bagreement = sig(m_Bagreement, sk_s) in
    let sig_Aagreement_server = sig((m_Aagreement, sig_Aagreement_client), sk_s) in
    (* g. encyrpt *)
    let m_5:bitstring = enc((nonce_2, m_uri, m_resource, m_Bagreement, sig_Bagreement, m_Aagreement, sig_Aagreement_client, sig_Aagreement_server), K) in
    (* h. send new message *)
    event serverHasAgreed(m_resource, m_Bagreement, m_Aagreement);
    event auth_ServerSendsLastMessageToClient(m_0, m_1, m_2, m_3, m_4, m_5);
    out (h,m_5);

    (* Server about to complete the protocol *)
    out(h, Server_Completes);
    if talksOnlyToHonest then
    (* event_at_the_very_end_of_role_R *)
    event auth_ServerCompletesProtocol(m_0, m_1, m_2, m_3, m_4);
    0.
(** END Server **)

(* END process macros *)

(* "Do the processes complete?" - sanity check*)
query attacker(Client_Completes).
query attacker(Server_Completes).


(* Checking for "Agreement" *)

(*
event_at_the_very_end_of_role_R
==>
event_just_before_the_last_send_message_of_S
*)

query msg_0:bitstring, msg_1:bitstring, msg_2:bitstring, msg_3:bitstring, msg_4:bitstring, msg_5:bitstring;
    event(auth_ClientCompletesProtocol(msg_0,msg_1,msg_2,msg_3,msg_4,msg_5))
    ==>
    event(auth_ServerSendsLastMessageToClient(msg_0,msg_1,msg_2,msg_3,msg_4,msg_5)) .


query msg_0:bitstring, msg_1:bitstring, msg_2:bitstring, msg_3:bitstring, msg_4:bitstring;
    event(auth_ServerCompletesProtocol(msg_0,msg_1,msg_2,msg_3,msg_4))
    ==>
    event(auth_ClientSendsLastMessageToServer(msg_0,msg_1,msg_2,msg_3,msg_4)) .

(* Checking for non-repudiation *)

(* 
event_client received resource 
==> event_server has agreed 
==> event_client has agreed 
==> event_server has offered  
*)

query resource:bitstring, policyA:bitstring, policyB:bitstring, shape:bitstring;
    inj-event(clientReceivedResource(resource, policyB, policyA))
    ==>
    (
        inj-event(serverHasAgreed(resource, policyB, policyA))
        ==>
        (
            inj-event(clientHasAgreed(shape, policyA, policyB))
            ==>
            inj-event(serverHasOffered(shape, policyA, policyB))
        )
    ) .



(* MAIN *)

free sk_e:SecretKey.
free sk_c:SecretKey [private].
free sk_s:SecretKey [private].

process (*use root process for setup *) 
    (*advertise public keys*)
    out(h,pk(sk_c));
    out(h,pk(sk_s));
    (*let agents dance*)
    ( (
         !client   (sk_c, pk(sk_s), true)
       | !client   (sk_c, pk(sk_e), false)
       | !server   (sk_s, pk(sk_c), true)
       | !server   (sk_s, pk(sk_e), false)
       (* dishonest clients and dishonest servers are impersonated by "the attacker" *)
    ) )