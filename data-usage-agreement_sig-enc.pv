(* This is a ProVerif description of the Protocol Flow from the Verifiable Consent architecture. *)

(* START keys *)
type SecretKey.
type PublicKey.
type SymmetricKey.
fun pk(SecretKey):PublicKey.
(* END keys *)

(* START group *)
type G.
type exponent.
fun exp(G, exponent): G.
fun g(exponent): G.
equation forall x: exponent, y: exponent; exp(g(x),y) = exp(g(y),x).
fun GtoSymK(G): SymmetricKey.
fun bitG(G):bitstring.
reduc forall elem:G; unbitG(bitG(elem))=elem.
(* END group *)

(* START symmetric encryption *)
fun enc(bitstring(*the message*), SymmetricKey):bitstring (*symmetric encrypt input msg*).
reduc forall msg:bitstring,symk:SymmetricKey; dec(enc(msg,symk),symk)=msg (*symmetric decrypt*).
(* END symmetric encryption *)

(* START digital signature *)
fun sig(bitstring(*the message*), SecretKey):bitstring(*the signature value*).
reduc forall msg:bitstring,sk:SecretKey; check(msg,sig(msg,sk),pk(sk))=true (*check(msg,sigVal,pk)*).
(* END digital signature *)

(* DEFS *)

(* Communication *)
free h:channel.
free m_uri:bitstring.
free m_A_offer:bitstring.
free m_B_request:bitstring.
free m_shape:bitstring.

fun ch(bitstring): channel.
free resource_is_private: bitstring [private].

(* Protocol Completes / Sanity checks *)
free Client_completes:bitstring [private].
free Server_completes:bitstring [private].

(* Sharing Events *)
event Client_received_resource(SecretKey, bitstring (* sk proves identity, m_resource *)).
event Server_provided_resource(SecretKey, bitstring (* sk proves identity, m_resource *)).
event Client_provided_credential(SecretKey, bitstring (* sk proves identity, m_presentation *)).
event Server_received_credential(SecretKey, bitstring (* sk proves identity, m_presentation *)).

(* Agreement Events *)
event Server_has_agreed(SecretKey, bitstring,bitstring (* sk_s, m_B_agreement, m_A_agreement_server *)).
event Client_has_agreed(SecretKey, bitstring,bitstring (* sk_c, m_A_agreement, m_B_requirement *)).
event Server_has_offered(SecretKey, bitstring,bitstring,bitstring (* sk_s, m_shape, m_A_offer, m_B_request *)).

(* Non-Repudiation Events *)
event Client_can_prove_server_has_offered(bitstring,bitstring,bitstring,bitstring,bitstring,bitstring (* m_shape, sig_shape, m_A_offer, sig_A_offer, m_B_request, sig_B_request *)).
event Client_can_prove_client_has_agreed(bitstring,bitstring,bitstring,bitstring (* m_A_agreement_client, sig_A_agreement_client, m_B_agreement, sig_B_agreement *)).
event Client_can_prove_server_has_agreed(bitstring,bitstring,bitstring,bitstring (* m_A_agreement_server, sig_A_agreement_server, m_B_agreement, sig_B_agreement *)).
event Server_can_prove_server_has_offered(bitstring,bitstring,bitstring,bitstring,bitstring,bitstring (* m_shape, sig_shape, m_A_offer, sig_A_offer, m_B_request, sig_B_request *)).
event Server_can_prove_client_has_agreed(bitstring,bitstring,bitstring,bitstring (* m_A_agreement_client, sig_A_agreement_client, m_B_requirement, sig_B_requirement *)).
event Server_can_prove_server_has_agreed(bitstring,bitstring,bitstring,bitstring (* m_A_agreement_server, sig_A_agreement_server, m_B_agreement, sig_B_agreement *)).



(* START process macros ~ agent *)

(** START Client **)
let client(sk_c:SecretKey, pk_s:PublicKey, talksOnlyToHonest:bool) (* input ~ what the agent starts with in private*) =

    (**)
    new x:exponent;
    let gx = g(x) in
    let m'_0 = bitG(gx) in
    let m_0 = m'_0 in
    out(h, m_0);
    (* Sent out Handshake-Request *)
    (**)
    (**)
    (* Receive Handshake-Response *)
    in(h,m_1:bitstring);
    let (eGY:bitstring,m:bitstring) = m_1 in
    let gy = unbitG(eGY) in
    let K = GtoSymK(exp(gy,x)) in
    let s_K = dec(m,K) in
    if check((gy,gx), s_K, pk_s) then 
    let m'_2 = (sig((gx,gy), sk_c), m_uri) in
    let m_2 = enc(m'_2, K) in
    out(h,m_2);
    (* Sent out data request (enc) *)
    (**)
    (**)
    (* Receive data usage offer (enc) *)
    in(h,m_3:bitstring);
    (* a. decrypt *)
    let m'_3 = dec(m_3,K) in
    (* b. deconstruct *)
    let (
        m'_uri:bitstring, 
        sig_shape:bitstring, 
        sig_A_offer:bitstring,
        sig_B_request:bitstring
        ) = m'_3 in
    (* c. check signatures *)
    if check(m_shape, sig_shape, pk_s) then
    if check(m_A_offer, sig_A_offer, pk_s) then
    if check(m_B_request, sig_B_request, pk_s) then
    (* d. check content *)
    if m'_uri = m_uri then
    (* e. create new message content *)
    let m_presentation:bitstring = m_shape in
    let m_A_agreement:bitstring = m_A_offer in
    let m_B_requirement:bitstring = m_B_request in
    (* f. sign files *)
    let sig_presentation:bitstring = sig(m_presentation, sk_c) in
    let sig_A_agreement_client:bitstring = sig(m_A_agreement, sk_c) in
    let sig_B_requirement:bitstring = sig(m_B_requirement, sk_c) in
    (* g. encrypt *)
    let m_4:bitstring = enc((m_uri, m_presentation, sig_presentation, m_A_agreement, sig_A_agreement_client, m_B_requirement, sig_B_requirement), K) in
    (* h. send new message *)
    event Client_has_agreed(sk_c, m_A_agreement, m_B_requirement);
    out(h,m_4);


    in(h,m_5:bitstring);
    (* a. decrypt *)
    let m'_5 = dec(m_5,K) in
    (* b. deconstruct *)
    let (
        m_resource:bitstring, 
        m_B_agreement:bitstring,
        sig_B_agreement:bitstring,
        m'_A_agreement:bitstring,
        sig'_Aagreement_client:bitstring,
        sig_A_agreement_server:bitstring
        ) = m'_5 in
    (* c. check signatures *)
    if check(m_B_agreement, sig_B_agreement, pk_s) then
    if check(m'_A_agreement, sig_A_agreement_server, pk_s) then
    (* d. check content *)
    if m_B_agreement = m_B_requirement then
    if m'_A_agreement = m_A_agreement then
    if sig'_Aagreement_client = sig_A_agreement_server then

    (* Client about to complete protocol! *)
    out(h, Client_completes);
    if talksOnlyToHonest then
    out(ch(m_resource),resource_is_private) |
    (* event_at_the_very_end_of_role_R *)    
    event Client_provided_credential(sk_c, m_presentation) |
    event Client_received_resource(sk_c, m_resource) |
    event Client_can_prove_server_has_offered(m_shape, sig_shape, m_A_offer, sig_A_offer, m_B_request, sig_B_request) |
    event Client_can_prove_server_has_agreed(m_A_agreement, sig_A_agreement_server, m_B_agreement, sig_B_agreement ) |
    event Client_can_prove_client_has_agreed(m_A_agreement, sig_A_agreement_client, m_B_agreement, sig_B_agreement);
    0.
(** END Client **)

(** START Server **)
let server(sk_s:SecretKey, pk_c:PublicKey, talksOnlyToHonest:bool) (* input ~ what the agent starts with in private*) =

    (**)
    (* Receive Handshake-Request *)
    in(h, m_0:bitstring);
    let gx= unbitG(m_0) in
    new y:exponent;
    let gy = g(y) in
    let K = GtoSymK(exp(gx,y)) in
    let m'_1 = sig((gy,gx),sk_s) in
    let m_1 = (bitG(gy),enc(m'_1, K)) in
    out(h,m_1); 
    (* Sent out Handshake-Response *)
    (**)
    (**)
    (* Receive data request (enc) *)
    in(h,m_2:bitstring);
    (* ab. decrypt deconstruct *)
    let (
        sig_K:bitstring, 
        m'_uri:bitstring
        ) = dec(m_2,K) in
    (* out-of-band - check if public *)
    (* c. check signatures *)
    if check((gx,gy),sig_K,pk_c) then
    (* d. check content *)
    if m'_uri = m_uri then
    (* e. create new message content *)
    (* f. sign message content *)
    let sig_shape:bitstring = sig(m_shape, sk_s) in
    let sig_A_offer:bitstring = sig(m_A_offer, sk_s) in
    let sig_B_request:bitstring = sig(m_B_request, sk_s) in
    (* g. encrypt *)
    let m_3:bitstring = enc((m_uri, m_shape, sig_shape, m_A_offer, sig_A_offer, m_B_request, sig_B_request), K) in
    (* h. send new message *)
    event Server_has_offered(sk_s, m_shape, m_A_offer, m_B_request);
    out(h, m_3);
    (* Sent out data usage offer (enc) *)


    in(h,m_4:bitstring);
    (* a. decyrpt *)
    let m'_4 = dec(m_4, K) in
    (* b. deconstruct *)
    let (
        m''_uri:bitstring, 
        m_presentation:bitstring, 
        sig_presentation:bitstring,
        m_A_agreement:bitstring,
        sig_A_agreement_client:bitstring,
        m_B_requirement:bitstring,
        sig_B_requirement:bitstring
        ) = m'_4 in
    (* c. check signatures *)
    if check(m_presentation, sig_presentation, pk_c) then
    if check(m_A_agreement, sig_A_agreement_client, pk_c) then
    if check(m_B_requirement, sig_B_requirement, pk_c) then
    (* d. check content *)
    if m''_uri = m_uri then
    if m_A_agreement = m_A_offer then
    if m_B_requirement = m_B_request then
    if m_presentation = m_shape then
    (* e. create new message content *)
    new m_resource:bitstring;
    let m_B_agreement:bitstring = m_B_requirement in
    (* f. sign message content *)
    let sig_B_agreement = sig(m_B_agreement, sk_s) in
    let sig_A_agreement_server = sig((m_A_agreement, sig_A_agreement_client), sk_s) in
    (* g. encyrpt *)
    let m_5:bitstring = enc((m_resource, m_B_agreement, sig_B_agreement, m_A_agreement, sig_A_agreement_client, sig_A_agreement_server), K) in
    (* h. send new message *)
    event Server_has_agreed(sk_s, m_B_agreement, m_A_agreement) |
    out (h,m_5);

    (* Server about to complete the protocol *)
    out(h, Server_completes);
    if talksOnlyToHonest then
    out(ch(m_resource),resource_is_private) |
    (* event_at_the_very_end_of_role_R *)
    event Server_received_credential(sk_s, m_presentation) |
    event Server_provided_resource(sk_s, m_resource) |
    event Server_can_prove_server_has_offered(m_shape, sig_shape, m_A_offer, sig_A_offer, m_B_request, sig_B_request) |
    event Server_can_prove_client_has_agreed(m_A_agreement, sig_A_agreement_client, m_B_requirement, sig_B_requirement) |
    event Server_can_prove_server_has_agreed(m_A_agreement, sig_A_agreement_server, m_B_agreement, sig_B_agreement);
    0.
(** END Server **)

(* END process macros *)

(* "Do the processes complete?" - sanity check*)
query attacker(Client_completes).
query attacker(Server_completes).
query attacker(resource_is_private).


(* Checking for "Agreement" *)

(*
event_at_the_very_end_of_role_R
==>
event_just_before_the_last_send_message_of_S
*)

(* query msg_0:bitstring, msg_1:bitstring, msg_2:bitstring, msg_3:bitstring, msg_4:bitstring, msg_5:bitstring;
    event(auth_ClientCompletesProtocol(msg_0,msg_1,msg_2,msg_3,msg_4,msg_5))
    ==>
    event(auth_ServerSendsLastMessageToClient(msg_0,msg_1,msg_2,msg_3,msg_4,msg_5)) .


query msg_0:bitstring, msg_1:bitstring, msg_2:bitstring, msg_3:bitstring, msg_4:bitstring;
    event(auth_ServerCompletesProtocol(msg_0,msg_1,msg_2,msg_3,msg_4))
    ==>
    event(auth_ClientSendsLastMessageToServer(msg_0,msg_1,msg_2,msg_3,msg_4)) . *)

(* Checking for non-repudiation *)

(* 
event_client received resource 
==> event_server has agreed 
==> event_client has agreed 
==> event_server has offered  
*)

(* query sk_c:SecretKey, sk_s:SecretKey, resource:bitstring, policyA:bitstring, policyB:bitstring, shape:bitstring, sig_policyA_client:bitstring, sig_policyA_server:bitstring, sig_policyB:bitstring;
    inj-event(Client_received_resource(resource, policyB, policyA))
    ==>
    inj-event(Server_has_agreed(sk_s, policyB, sig_policyB, policyA, sig_policyA_client, sig_policyA_server))
    &&
    inj-event(Client_has_agreed(sk_c, shape, policyA, policyB))
    &&
    inj-event(Server_has_offered(sk_s, shape, policyA, policyB))
    . *)

query sk_s:SecretKey, policyB:bitstring, policyA:bitstring, sig_policyA:bitstring, sig_policyB:bitstring;
    inj-event(Client_can_prove_server_has_agreed(policyA,sig_policyA,policyB,sig_policyB))
    ==> 
    inj-event(Server_has_agreed(sk_s,policyA,policyB))
    .

query sk_c:SecretKey, policyB:bitstring, policyA:bitstring, sig_policyA:bitstring, sig_policyB:bitstring;
    inj-event(Server_can_prove_client_has_agreed(policyA,sig_policyA,policyB,sig_policyB))
    ==> 
    inj-event(Client_has_agreed(sk_c,policyA,policyB))
    .

query sk_s:SecretKey, policyB:bitstring, policyA:bitstring, sig_policyA:bitstring, sig_policyB:bitstring, credential:bitstring, sig_credential:bitstring;
    inj-event(Client_can_prove_server_has_offered(credential,sig_credential,policyA,sig_policyA,policyB,sig_policyB))
    ==> 
    inj-event(Server_has_offered(sk_s,credential,policyA,policyB))
    .

query sk_s:SecretKey, policyB:bitstring, policyA:bitstring, sig_policyA:bitstring, sig_policyB:bitstring, credential:bitstring, sig_credential:bitstring;
    inj-event(Server_can_prove_server_has_offered(credential,sig_credential,policyA,sig_policyA,policyB,sig_policyB))
    ==> 
    inj-event(Server_has_offered(sk_s,credential,policyA,policyB))
    .

query sk_s:SecretKey, policyB:bitstring, policyA:bitstring, sig_policyA:bitstring, sig_policyB:bitstring;
    inj-event(Server_can_prove_server_has_agreed(policyA,sig_policyA,policyB,sig_policyB))
    ==> 
    inj-event(Server_has_agreed(sk_s,policyA,policyB))
    .

query sk_c:SecretKey, policyB:bitstring, policyA:bitstring, sig_policyA:bitstring, sig_policyB:bitstring;
    inj-event(Client_can_prove_client_has_agreed(policyA,sig_policyA,policyB,sig_policyB))
    ==> 
    inj-event(Client_has_agreed(sk_c,policyA,policyB))
    .

(* IF client received, the server has offered and agreed *)
query sk_c:SecretKey, sk_s:SecretKey, credential:bitstring,policyA:bitstring, policyB:bitstring, resource:bitstring;
    inj-event(Client_received_resource(sk_c, resource))
    ==>
    inj-event(Server_has_agreed(sk_s,policyA,policyB))
    &&
    inj-event(Server_has_offered(sk_s,credential,policyA,policyB))
    .

(* IF server served, the client has agreed *)
 query sk_c:SecretKey, sk_s:SecretKey, credential:bitstring,policyA:bitstring, policyB:bitstring, resource:bitstring;
    inj-event(Server_provided_resource(sk_s,resource))
    ==> 
    inj-event(Client_has_agreed(sk_c,policyA,policyB))
    .

(* IF protocol fullfilled, everyone agreed *)
query sk_c:SecretKey, sk_s:SecretKey, credential:bitstring,policyA:bitstring, policyB:bitstring, resource:bitstring;
    inj-event(Client_received_resource(sk_c,resource))
    ==>
    inj-event(Server_has_agreed(sk_s,policyA,policyB))
    &&
    inj-event(Client_has_agreed(sk_c,policyA,policyB))
    &&
    inj-event(Server_has_offered(sk_s,credential,policyA,policyB))
    . 

query sk_c:SecretKey, sk_s:SecretKey, credential:bitstring,policyA:bitstring, policyB:bitstring, resource:bitstring;
    inj-event(Server_provided_resource(sk_s,resource))
    ==>
    inj-event(Server_has_agreed(sk_s,policyA,policyB))
    &&
    inj-event(Client_has_agreed(sk_c,policyA,policyB))
    &&
    inj-event(Server_has_offered(sk_s,credential,policyA,policyB))
    . 


(* MAIN *)

free sk_e:SecretKey.
free sk_c:SecretKey [private].
free sk_s:SecretKey [private].

process (*use root process for setup *) 
    (*advertise public keys*)
    out(h,pk(sk_c));
    out(h,pk(sk_s));
    (*let agents dance*)
    ( (
         !client   (sk_c, pk(sk_s), true)
       | !client   (sk_c, pk(sk_e), false)
       | !server   (sk_s, pk(sk_c), true)
       | !server   (sk_s, pk(sk_e), false)
       (* dishonest clients and dishonest servers are impersonated by "the attacker" *)
    ) )